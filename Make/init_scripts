
# It is easiest if this file does not emit any output. 
#
# This file cannot assume that IVI_SRC_PATH is set, as root does not use
# IVI_SRC_PATH. But it does assume that we have MAKE_PATH.
#
# This file contains overlap between what general scripts need and what
# compiling needs. It is sourced from init_compile, and possibly from other
# scripts, and often from .cshrc/.tcshrc.
#
# Rather than select exactly the overlap, we include all members of a logical
# group, e.g., all variables about Make paths or general system commands. 


# Invariably we have IVI_HAVE_MACHINE, but if we do not, the we need to source
# init_machine as to be standalone. This could be a different copy than the one
# that build sources before sourcing this file (init_scripts).
# 
if (! $?IVI_HAVE_MACHINE) then
    source ${MAKE_PATH}init_machine
    if ($status) exit $status
endif 

if (! $?IVI_WARN_LEVEL) then
    setenv IVI_WARN_LEVEL 2
endif

# if (-e /dev/stderr) then
#     setenv P_STDERR "echo > /dev/stderr"
# else 
    setenv P_STDERR "${MAKE_PATH}scripts/p_stderr"
# endif 

# For single lines of verbose echoes. For blocks of text, use an "if" as usual. 
# Kobus needs to sync this code with duplicated code in his .cshrc
if ($?IVI_VERBOSE) then
    setenv VERBOSE_ECHO "echo "
else if ($?tcsh) then
    setenv VERBOSE_ECHO ": "
else 
    setenv VERBOSE_ECHO "eval echo > /dev/null "
endif 

# For single lines of verbose echoes. For blocks of text, use an "if" as usual. 
# Kobus needs to sync this code with duplicated code in his .cshrc
if ($?IVI_VERBOSE_2) then
    setenv VERBOSE_ECHO_2 "echo "
else if ($?tcsh) then
    setenv VERBOSE_ECHO_2 ": "
else 
    setenv VERBOSE_ECHO_2 "eval echo > /dev/null"
endif 

# Kobus needs to sync this code with duplicated code in his .cshrc
if (($?prompt) && (! $?IVI_QUIET)) then
    setenv ECHO "echo "
else if ($?tcsh) then
    setenv ECHO ":  "
else 
    setenv ECHO "eval echo > /dev/null"
endif 

if(! $?TMPDIR) then
    setenv TMPDIR "/tmp"
endif

# --------------------------------------------------------------------------------------------

setenv IVI_ECHO             "echo"
setenv IVI_CAT              "cat"

# Copy where links point to, not their contents. 
if ("${OS}" == "mac_osx") then
    setenv IVI_CP               "cp -f -R"
else if (("${OS}" == "linux_x86_64") || ("${OS}" == "linux_386")) then
    setenv IVI_CP               "cp -r -f -d"
else
    setenv IVI_CP               "cp -r -f"
endif 

setenv IVI_CREATE           "cp /dev/null"
setenv IVI_GREP             "grep"
setenv IVI_EGREP            "egrep"
setenv IVI_FILE             "file"
# A version of ls that outputs one file per line. Most if not all ls's do so
# when the output is not a terminal. However, we can use the '-1' option for
# clarity and robustness.
setenv IVI_LS               "ls -1"
setenv IVI_LN               "ln -s -f"
setenv IVI_MV               "mv"
setenv IVI_RM               "rm -r -f"
setenv IVI_TOUCH            "touch"
setenv IVI_TOUCH_1990       "touch -t 199001010000"
setenv IVI_PERMIT_EXECUTION "chmod -R a+rx"
setenv IVI_PERMIT_READ      "chmod -R a+rX"
setenv IVI_MKDIR            "mkdir -p"
setenv IVI_SED              "sed"

# setenv IVI_COMPRESS bzip2
# setenv IVI_COMPRESS_SUFFIX bz2
#
# Back to gzip --- It does not compress quite as well, but is more convenient
#
setenv IVI_COMPRESS             "gzip"
setenv IVI_COMPRESS_SUFFIX      "gz"

# ---------------------------------------------------------------------

# Perhaps this should be made more efficient now that we do not cache IVI_STAT
# and IVI_STAT_FLAVOR. 

set test_paths = `echo ${PATH} | sed 's/:/ /g'`
   
setenv IVI_STAT "stat"
setenv IVI_STAT_FLAVOR "NONE"

set have_stat = 0 

foreach test_path ( $test_paths )
    if (-x ${test_path}/stat) then
        set have_stat = 1
        break
    endif
end

if (${have_stat}) then
    set gnu_stat = `${IVI_STAT} --version |& grep 'GNU coreutils'`
    if ((! ${status} ) && ("${gnu_stat}" != "")) then
        setenv IVI_STAT_FLAVOR "gnu"
    else 
        set bsd_stat = `${IVI_STAT} -s . |& grep 'st_mtime'`
        if ((! ${status} ) && ("${bsd_stat}" != "")) then
            setenv IVI_STAT_FLAVOR "bsd"
        endif
    endif
endif

# ---------------------------------------------------------------------

setenv IVI_HAVE_INIT_SCRIPT 1 

