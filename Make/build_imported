#!/bin/tcsh -f

# Not recently TESTED, but quite a few changes have been made! 

##################################################################################
#
# Build script for imported libraries and utilities where mimimal build
# infrastracture exists. Unless the code is very simple, usually there is some
# infrastructure, and typically we want to leverage that using a different
# script (perhaps yet to be written). However: 
#    For code that comes with "configure", use the template build_with_configure
#    For code that comes with "cmake", use the template build_with_cmake
#
# ================================================================================
#
# This file started its life as a copy of 
#     ${KJB_SRC_PATH}/Make/scripts/build_imported
# Typically it gets renamed as "build" or "build_ivilab". Sometimes it gets some
# tweaks for the particular libraries. 
#
# The file
#     ${KJB_SRC_PATH}/Make/scripts/build_imported
# is a template for a build script for imported library code which comes with
# some makefiles which we typically move to Makefile-2, and tweak. Having done
# that, we can (optionally) add a copy of  ${KJB_SRC_PATH}/Make/scripts/Makefile. 
#
#
# This file is generally called by "make". It can also be called directly. The
# main purpose is to normalize the make environment, and to implement some
# functionality that is difficult to do in a robust portable way for all flavors
# of make. 
#
# The chief functionallity implemented here is to determine the value of
# KJB_SRC_PATH, and then call build-2 to continue setting up the environment,
# and the call make with "-f Makefile-2", but often that part will
# be replaced with the specific commands for the imported package. We tell
# build-2 not to build additional makefiles by setting KJB_SKIP_DEPEND.
#
##################################################################################
#

# This next part is shared by all files in Make whose names begin with "build",
# AND kjb_add_makefiles. Updates should be propogated. 

if ($?FORCE_KJB_SRC_DIR) then 
    if (-e "${FORCE_KJB_SRC_DIR}/Make/init_compile") then 
        setenv KJB_SRC_PATH "${FORCE_KJB_SRC_DIR}"
    else 
        bash -c 'echo "Env var FORCE_KJB_SRC_DIR is set to ${FORCE_KJB_SRC_DIR}, but ${FORCE_KJB_SRC_DIR}/Make/init_compile does not exist." >&2'
        exit 1 
    endif 
else  
    # Use env var for KJB_DIRS for better error reporting via bash sub-shell.
    if ($?FORCE_KJB_DIR) then
        setenv KJB_DIRS "${FORCE_KJB_DIR}"
    else
        setenv KJB_DIRS ". kjb KJB ivi IVI src"
    endif 

    # Find out where we are in the source tree. In particular, we want to find a
    # directory that has one of the directories in KJB_DIRS as a subdir, which
    # itself has Make as a sub-dir and Make/init_compile as a file in that. 

    set found = 0
    set kjb_top = ""

    pushd `pwd` > /dev/null

    while ("`pwd`" != "/")
        foreach kjb_dir (${KJB_DIRS}) 
            if ("${kjb_dir}" == ".") then
                set kjb_dir = ""
            else
                set kjb_dir = "${kjb_dir}/"
            endif 

            if (-e "${kjb_dir}Make/init_compile") then
                set found = 1
                break
            endif 
        end

        if (${found}) break

        set kjb_top = "../${kjb_top}"

        cd ..
    end

    popd > /dev/null

    if (${found}) then
        if ("${kjb_dir}" == "") then
            set kjb_dir = "./"
        endif 

        setenv KJB_SRC_PATH "${kjb_top}${kjb_dir}"
    else 
        # We cannot use P_STDERR here because we might not have defined it yet.
        bash -c 'echo " " >&2'
        bash -c 'echo "This directory is not below a directory with KJB installed." >&2'
        bash -c 'echo "Valid directories for KJB in this context: ${KJB_DIRS}." >&2'
        bash -c 'echo "To experiment with a temporary fix, try setting FORCE_KJB_SRC_DIR." >&2'
        bash -c 'echo "For a more permanent fix, edit $0 or contact Kobus." >&2'
        exit 1
    endif 
endif 

pushd ${KJB_SRC_PATH} > /dev/null
    setenv KJB_SRC_PATH "${cwd}/"
popd > /dev/null

# Check that this file ($0) is up to date compared to the reference one.
set need_to_replace_myself = `${KJB_SRC_PATH}Make/scripts/test_older_than "$0" "${KJB_SRC_PATH}Make/build"`
     
if (${need_to_replace_myself}) then
    # We could make this even more automated using the defunct script
    # self_update, but this is too fancy. It cannot do the rebuild in parallel,
    # and we would want to implement checking for infinite recursion.  Using
    # self_update is commented out here:
    #     exec ${KJB_SRC_PATH}Make/scripts/self_update "$0" "${KJB_SRC_PATH}Make/build" Make

    echo "Script $0 is attempting to rebuild itself."
    echo "You will need to reenter your build command (e.g., make or $0)."
    exec /bin/cp "${KJB_SRC_PATH}Make/build" "$0"
endif 

setenv KJB_SKIP_DEPEND
setenv KJB_SKIP_DIR_MADE
source ${KJB_SRC_PATH}Make/scripts/build-2 



