#!/bin/tcsh -f

##################################################################################
#
# This script builds the file Include_lines/sub_libs_needed in the directory
# given by the first parameter. If that parameter is not set or empty, then we
# build in the current directory. The file Include_lines/sub_libs_needed
# contains a single line that lists sub-directories needed by the code in the
# requested directory ordered so that sub-directory are preceded by all
# sub-directories that it might depend on. 
#
# Here 'needed' goes beyond what can be found by recursively examining header
# files, which is what the tool 'makedepend' does.  We want to know about
# dependencies due to C files in library sub-dirs. Further we want the library
# sub-directories to be ordered so that dependency is expected. In particular,
# we order the sub-dirs so that all items have all their dependencies preceding
# them. For various tasks the script build_makefile_dirs will use the list in
# either forward or reverse order. Note that this assumes that the IVI library
# has a tree structured dependency graph. If this is not the case, then circular
# dependencies will be identified by this script. True circular dependencies
# need to be corrected. 
#
# To proceed, we find all sub-libs needed by the code in the requested
# directory. We then prepend that list with content of
# Include_lines/sub_libs_needed in each directory in the list. This requires
# ensuring that these Include_lines/sub_libs_needed are up to date, and building
# them if needed. Hence this script may be called recursively. Currently this is
# via either by a call to build_include_lines_2 which will call this script,

if ($?VERBOSE_INDENT) then
    setenv VERBOSE_INDENT "    ${VERBOSE_INDENT}"
else 
    setenv VERBOSE_INDENT ""
endif 

@ num_args = ${#argv}

if ("${num_args}" > 2) then
    ${P_STDERR} "Script build_sub_libs_needed takes only 0, 1, or 2 parameters."
    exit 1 
endif 

set build_dir = ""

if ("${num_args}" >= 1) then
    if ("${1}" != "") then
        set build_dir = `echo ${1} | sed 's/ *//g'`
    endif 
endif 

if ("${build_dir}" != "") then
    set build_dir = `echo ${build_dir} | sed 's#//*#/#g' | sed 's#/$##'`
    pushd ${build_dir} > /dev/null
    # We are now in the build directory. (No popd). 
endif 

# This might be OBSOLETE. 
if ("${num_args}" >= 2) then
    set parent_sub_lib = "${2}"
else  
    set parent_sub_lib = ""
endif 

set current_sub_lib = `echo ${cwd} | sed 's#.*/lib/\([^/]*\)$#\1#'`

if ("${current_sub_lib}" == "${cwd}") then
    set current_sub_lib = ""
endif 

#########################################################

if (! $?TMP_SUB_DIRS_VISITED) then
    setenv TMP_SUB_DIRS_VISITED ""
endif 

#########################################################

${VERBOSE_ECHO} " "
${VERBOSE_ECHO} "${VERBOSE_INDENT}Beginning recursive build of ${cwd}/Include_lines/sub_libs_needed"

${VERBOSE_ECHO} "${VERBOSE_INDENT}Entering script build_sub_libs_needed with directory ${cwd}"
${VERBOSE_ECHO} "${VERBOSE_INDENT}    build_dir: ${build_dir}"
${VERBOSE_ECHO} "${VERBOSE_INDENT}    parent_sub_lib: ${parent_sub_lib}"
${VERBOSE_ECHO} "${VERBOSE_INDENT}    current_sub_lib: ${current_sub_lib}"
${VERBOSE_ECHO} "${VERBOSE_INDENT}    TMP_SUB_DIRS_VISITED: ${TMP_SUB_DIRS_VISITED}"
${VERBOSE_ECHO} " "

#########################################################

if ("${current_sub_lib}" != "") then
    set circular = 0
    foreach tmp_sub_dir_visited (${TMP_SUB_DIRS_VISITED})
        if ("${tmp_sub_dir_visited}" == "${current_sub_lib}") then
            set circular = 1
            ${P_STDERR} " "
            ${P_STDERR} "Circular dependency spotted."
            echo -n "    ${tmp_sub_dir_visited} depends on " > /dev/stderr
            continue
        endif 

        if (${circular}) then 
            ${P_STDERR} "${tmp_sub_dir_visited}"
            echo -n "    ${tmp_sub_dir_visited} depends on " > /dev/stderr
        endif
    end

    if (${circular}) then 
        ${P_STDERR} "${current_sub_lib}"
        ${P_STDERR} " "
#         echo "Removing ${cwd}/Include_lines/sub_lib_list"
#         ${IVI_RM} "${cwd}/Include_lines/sub_lib_list"
#         echo " "

        exit 1
    endif
endif 

setenv TMP_SUB_DIRS_VISITED "${TMP_SUB_DIRS_VISITED} ${current_sub_lib}"

#########################################################

if (-e Include_lines/sub_lib_list) then
    if ("${current_sub_lib}" != "") then
        set skip_str = "\<${current_sub_lib}\>"
        set recurse_dirs = `cat Include_lines/sub_lib_list | grep -v ${skip_str}`
    else
        set recurse_dirs = `cat Include_lines/sub_lib_list`
    endif 
else 
    ${P_STDERR} "Script build_sub_libs_needed requires Include_lines/sub_lib_list"
    ${P_STDERR} "Error occurred in directory $cwd"
    exit 1
endif 

${VERBOSE_ECHO} "${VERBOSE_INDENT}Recurse dirs in ${cwd} are: " 
${VERBOSE_ECHO} "${VERBOSE_INDENT}    ${recurse_dirs}" 

if (! $?FORCE_SERIAL) then
    ${MAKE_SCRIPT_PATH}build_sub_libs_needed_parallel ${recurse_dirs}
    if (${status}) exit ${status}
else
    ${MAKE_SCRIPT_PATH}build_sub_libs_needed_serial ${recurse_dirs}
    if (${status}) exit ${status}
endif 

# ${MAKE_SCRIPT_PATH}build_sub_libs_needed_parallel ${recurse_dirs}
# ${MAKE_SCRIPT_PATH}build_sub_libs_needed_serial ${recurse_dirs}

${VERBOSE_ECHO} "${VERBOSE_INDENT}Leaving script build_sub_libs_needed in ${cwd} with success."

