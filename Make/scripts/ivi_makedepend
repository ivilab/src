#!/bin/tcsh 

# Careful with echo as this script works by writing to standard out. All echo
# output should be sent to /dev/stderr. 

# set echo 

set num_args = $#argv

if (${num_args} < 2) then
    ${P_STDERR} "Script ivi_makedepend needs at least two arguments."
    exit 1 
endif 

# All meta output must go to stderr, but using the symbol P_STDERR inserts the
# prefix "PSE: " which can be confusing. So, we just go for the native version.
#
if ($?IVI_VERBOSE) then
    ${MAKE_SCRIPT_PATH}p_stderr "IVB: " " "
    ${MAKE_SCRIPT_PATH}p_stderr "IVB: " "${VERBOSE_INDENT}Script ivi_makedepend called with arguments: $1 $2"
    ${MAKE_SCRIPT_PATH}p_stderr "IVB: " "${VERBOSE_INDENT}in directory `pwd`."
endif

set dir = "$1"
set suffix_str = "$2"

# echo dir:${dir} > ivi_makedepend_dir_arg

set c_source = 0
set cxx_source = 0

if ("${suffix_str}" == "-c") then
    set c_source = 1
else if ("${suffix_str}" == "-x") then
    set cxx_source = 1
else 
    ${P_STDERR} "First argument: ${arg} to script ivi_makedepend is invalid."
    exit 1 
endif 

set rest_of_args = "$argv[3-$num_args]"
# echo rest_of_args: ${rest_of_args}


if ("${dir}" != "") then 
    pushd ${dir} > /dev/null
endif 

${IVI_CAT} /dev/null > Makefile-depend-tmp
if ("${status}") exit 1

set files = `${MAKE_SCRIPT_PATH}stream_file_names ${suffix_str}`

if ("${files}" != "") then
    ${IVI_CAT} /dev/null > Makefile-depend-tmp-2
    if ("${status}") exit 1

    which ${MAKEDEPEND} >& /dev/null
  
    if (${status}) then
        ${P_STDERR} "Script ivi_makedepend cannot find 'makedepend'"
        ${P_STDERR} "Perhaps it is not installed on this system?"
        exit 1
    endif 

    # All meta output must go to stderr, but using the symbol P_STDERR inserts
    # the prefix "PSE: " which can be confusing. So, we just go for the native
    # version.
    #
    if ($?IVI_VERBOSE) then
        ${MAKE_SCRIPT_PATH}p_stderr "IVB: "
        ${MAKE_SCRIPT_PATH}p_stderr "IVB: ${VERBOSE_INDENT}Script ivi_makedepend is executing: "
        ${MAKE_SCRIPT_PATH}p_stderr "IVB: ${VERBOSE_INDENT}    ${MAKEDEPEND} -f Makefile-depend-tmp-2 ${rest_of_args} ${files}"
        ${MAKE_SCRIPT_PATH}p_stderr "IVB: ${VERBOSE_INDENT}in directory `pwd`."
    endif

    (${MAKEDEPEND} -f Makefile-depend-tmp-2 ${rest_of_args} ${files} > /dev/null) >& Include_lines/makedepend.err

    if ("${status}") then
        ${P_STDERR} "Program makedepend failed in ivi_makedepend in ${cwd}."
        ${P_STDERR} "Any error output should be in Include_lines/makedepend.err"
        ${P_STDERR} " "
        exit 1
    endif

    if ($?IVI_VERBOSE) then
        ${MAKE_SCRIPT_PATH}p_stderr "IVB: ${VERBOSE_INDENT}Done ivi_makedepend in ${cwd}."
        ${MAKE_SCRIPT_PATH}p_stderr " "
    endif

    if ("${dir}" != "") then
        if ($?IVI_DEBUG) then
            ${IVI_CP} Makefile-depend-tmp-2 Makefile-depend-tmp-2${suffix_str}-SAVE
            if ("${status}") exit 1
        endif 
        
        sed "s#^\([A-Za-z0-9_\-]\)#${dir}\1#" Makefile-depend-tmp-2 > Makefile-depend-tmp-3
        if ("${status}") exit 1

        ${IVI_MV} Makefile-depend-tmp-3  Makefile-depend-tmp-2
        if ("${status}") exit 1
    endif  

    if ($?IVI_DEBUG) then
        ${IVI_CP} Makefile-depend-tmp-2 Makefile-depend-tmp-2${suffix_str}-SAVE-2
        if ("${status}") exit 1
    endif 

    ${IVI_RM} .Makefile-depend-tmp-2.swp
    if ("${status}") exit 1

    ${VIM_FOR_MAKE} Makefile-depend-tmp-2 < ${MAKE_EDIT_PATH}Dirs.${VIM_SUFFIX} >& /dev/null
    if ("${status}") exit 1

    if ($?IVI_DEBUG) then
        ${IVI_CP} Makefile-depend-tmp-2 Makefile-depend-tmp-2${suffix_str}-SAVE-3
        if ("${status}") exit 1
    endif 

    ${IVI_RM} .Makefile-depend-tmp-2.swp
    if ("${status}") exit 1

    ${VIM_FOR_MAKE} Makefile-depend-tmp-2 < ${MAKE_EDIT_PATH}Depend.${VIM_SUFFIX} >& /dev/null
    if ("${status}") exit 1

    if ($?IVI_DEBUG) then
        ${IVI_CP} Makefile-depend-tmp-2 Makefile-depend-tmp-2${suffix_str}-SAVE-4
        if ("${status}") exit 1
    endif 

    if (${c_source}) then
        sed 's/\([a-z0-9A-Z_\-\.]*\)\.o *:/$(OBJ_DIR)\1.o :/' Makefile-depend-tmp-2 > Makefile-depend-tmp 
    else if (${cxx_source}) then
        sed 's/\([a-z0-9A-Z_\-\.]*\)\.o *:/$(CXX_OBJ_DIR)\1.o :/' Makefile-depend-tmp-2 > Makefile-depend-tmp 
    endif 

    if ("${status}") exit 1

    ${IVI_RM} Makefile-depend-tmp-2
    ${IVI_RM} Makefile-depend-tmp-2.bak
endif 

${IVI_CAT} Makefile-depend-tmp
if ("${status}") exit 1

${IVI_RM}  Makefile-depend-tmp
${IVI_RM}  makedepend.err



