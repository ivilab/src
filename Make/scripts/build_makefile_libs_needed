#!/bin/tcsh -f

###############################################################################
#
# Identification:
#     A script to build the file Makefile-libs-needed
#
# Description:
#     This script builds the file Makefile-libs-needed (technically,
#     Include_lines/Makefile-libs-needed/Makefile-libs-needed${VAR_CACHE_SUFFIX}) 
#     in the directory given by the first parameter, or the current directory if
#     there are no parameters. 
#
#     This script might be superflous as it used to setup locking for
#     build_makefile_libs_needed_2, but now we do it outside.
#
# Author:
#     Kobus Barnard
#
###############################################################################

if ($?IVI_SET_ECHO) then
    set echo    
endif 

@ num_args = ${#argv}

if ("${num_args}" > 1) then
    ${P_STDERR} "Script build_makefile_libs_needed takes only  0 or 1 parameters."
    exit 1
endif 

##########################################################

if ($?VERBOSE_INDENT) then
    setenv VERBOSE_INDENT "    ${VERBOSE_INDENT}"
else 
    setenv VERBOSE_INDENT ""
endif 

##########################################################

${VERBOSE_ECHO} "${VERBOSE_INDENT}Entering script build_makefile_libs_needed in directory ${cwd}."

set build_dir = ""

if ("${num_args}" >= 1) then
    if ("${1}" != "") then
        set build_dir = `echo ${1} | sed 's/ *//g'`
    endif 
endif 

if ("${build_dir}" != "") then
    set build_dir = `echo ${build_dir} | sed 's#//*#/#g' | sed 's#/$##'`
    cd ${build_dir} 
endif 

${MAKE_SCRIPT_PATH}build_makefile_libs_needed_2 $* 
set exit_status = ${status}

${VERBOSE_ECHO} "${VERBOSE_INDENT}Leaving script build_makefile_libs_needed in directory $cwd with status ${exit_status}."

