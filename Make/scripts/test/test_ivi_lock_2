#!/bin/tcsh -f

# We assume that we have sourced init_compile. 

set num_procs = 5
set num_iterations = 10 

@ num_args = ${#argv}

if ($num_args > 1) then
    set num_iterations = $2
endif 

if ($num_args > 0) then
    set num_procs = $1
endif 

if ($?TEST_LOCK_NO_LOCK) then
   setenv LOCK_PROGRAM ""
else 
   setenv LOCK_PROGRAM "${MAKE_SCRIPT_PATH}ivi_lock"
endif 

setenv TEST_LOCK_RESULT_FILE "${0}.result"

set test_lock_output_file = "${0}.output"
set test_lock_answer_file = "${0}.answer"
set test_lock_check_file = "${0}.check"

${IVI_CREATE} "${TEST_LOCK_RESULT_FILE}"
${IVI_CREATE} "${test_lock_output_file}"
${IVI_CREATE} "${test_lock_answer_file}"

@ proc_count = 0

while ($proc_count < $num_procs)
    ./test_ivi_lock_2_helper $num_iterations !>>& "${test_lock_output_file}" & 
    @ proc_count += 1
    echo proc_count: $proc_count
end 

wait 

@ num_total_iterations = $proc_count * $num_iterations

@ base = 10001
@ iteration_count = 0
while ($iteration_count < $num_total_iterations)
    @ base_plus_count = $base + $iteration_count
    echo $base_plus_count >>! ${test_lock_answer_file}
    @ iteration_count += 1
end 

sed < "${TEST_LOCK_RESULT_FILE}" 's/ .*$//' > "${test_lock_check_file}"

diff -q ${test_lock_answer_file} ${test_lock_check_file}

if (${status}) then
    # echo "Files ${test_lock_check_file} and ${test_lock_answer_file} differ."
    exit 1
endif 

 




