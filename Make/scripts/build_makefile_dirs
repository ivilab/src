#!/bin/tcsh -f

################################################################################
# 
# This script builds a file called Makefile-dirs in the directory given by the
# last parameter which gets bound to the variable build_dir. If that parameter
# is not set or emply, then we build in the current directory. 
#
# Much of what is needed for this file comes from Include_lines/sub_libs_needed.
# We first build the part that can be done without
# Include_lines/sub_libs_needed. If Include_lines/sub_libs_needed exists, we
# then create entries for each of the sub libs. Note that the items in
# Include_lines/sub_libs_needed are ordered so that all items have all their
# dependencies preceeding them. 
#
# We export symbols for directory separator (e.g., '/') so as not to restrict to
# unix (although it has been a while since this has been relavent). 
#
################################################################################

# set echo

@ num_args = ${#argv}

# echo num_args: ${num_args}

if ("${num_args}" > 1) then
    ${P_STDERR} "Script build_makefile_dirs needs 0 or 1 parameters."
    exit 1
endif 

if ($?IVI_VERBOSE) then
    if ($?VERBOSE_INDENT) then
        setenv VERBOSE_INDENT "    ${VERBOSE_INDENT}"
    else 
        setenv VERBOSE_INDENT ""
    endif 

    set ppid = `ps -p $$ -o ppid=`
    if (${status}) exit ${status}

    echo "IVB: ${VERBOSE_INDENT}Process $$ with parent ${ppid} entering script build_makefile_dirs in directory ${cwd} with args '$*'."
endif

set build_dir = ""
# set dir_arg = ""

if ("${num_args}" >= 1) then
    if ("${1}" != "") then
        # set dir_arg = "$1"
        set build_dir = `echo ${1} | sed 's/ *//g'`
    endif 
endif 

if ("${build_dir}" != "") then
    set build_dir = `echo ${build_dir} | sed 's#///*#/#g' | sed 's#/$##'`
    cd ${build_dir}
endif 

if ($?IVI_VERBOSE) then
    set start_time = `date +%H:%M:%S`
endif 

${MAKE_SCRIPT_PATH}ivi_lock 120 ${MAKE_SCRIPT_PATH}build_makefile_dirs_2 
set exit_status = ${status}

if ($?IVI_VERBOSE) then
    set end_time = `date +%H:%M:%S`
    echo "IVB: ${VERBOSE_INDENT}Script build_makefile_dirs finished"
    echo "IVB: ${VERBOSE_INDENT}    Start time: ${start_time}"
    echo "IVB: ${VERBOSE_INDENT}    End time:   ${end_time}"
    echo "IVB:  "
    echo "IVB: ${VERBOSE_INDENT}Process $$ with parent ${ppid} leaving script build_makefile_dirs in directory ${cwd} with status ${exit_status}."
endif 

exit ${exit_status} 

