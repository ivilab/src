#!/bin/tcsh -f 

##############################################################################
#
# Identification:
#     Creates a semaphore that blocks cooperating processes from proceeding 
#     until removed (using ivi_lock_remove).
#
# Description:
#     This is a simple script for locking blocks of script code that is not
#     conveniently expressed as a command (otherwise use ivi_lock). It works
#     much like lockfile-create, and in fact, is implmented using
#     lockfile-create if it is available. The main justification for this script
#     is that locking is not universally available. Most notably good locking
#     facilities are not available in the mac OS. Hence, this script also
#     implements a fall back method that is less than ideal, but usually works. 
#
#     The argument to ivi_lock_create is simply a label (often just the name of the
#     script), which is combined with the current directory to create a lockfile
#     name. At the end of the protected code, you mjust call ivi_lock_remove
#     with exactly the same argument. 
#
# Deficiencies
#     The current implementation locks all runs of a command operating in a
#     given sub-directory, which is is not a big issue as it is simply overly
#     conservative. If this becomes an issue, we can put more path components in
#     the lockfile name. 
#
# Author:
#     Kobus Barnard 
#
##############################################################################

# This script must be kept in sync with ivi_lock_remove

# set echo

if ($# < 1) then
    ${P_STDERR} "Script ivi_lock_create needs at least one argument."
    exit 1
endif 

set lock_label = "$1"

set exit_status = 0

if ($?IVI_VERBOSE) then
    if ($?IVI_VERBOSE_INDENT) then
        set verbose_indent = "${IVI_VERBOSE_INDENT}"
    else
        set verbose_indent = ""
    endif
endif 

if ($?IVI_DONT_LOCK) then
    ${VERBOSE_ECHO} "${verbose_indent}IVI_DONT_LOCK is set, so ivi_lock_create is not locking: $*"
    exit 0
endif

if (! $?IVI_LOCKING_METHOD) then
    # Sync this code with that in init_compile!

    if ((`${MAKE_SCRIPT_PATH}can_execute lockfile-create`) && (`${MAKE_SCRIPT_PATH}can_execute lockfile-remove`)) then
        setenv IVI_LOCKING_METHOD "lockfile-progs" 
    else
        setenv IVI_LOCKING_METHOD "fallback"
    endif 
endif 

if (! $?IVI_LOCKFILE_DIR) then
    setenv IVI_LOCKFILE_DIR ${TMPDIR}/${USER}/lockfiles/
endif 

${IVI_MKDIR} "${IVI_LOCKFILE_DIR}"

if ($status) then
    $P_STDERR "Unable to make directory ${IVI_LOCKFILE_DIR}"
    exit 1
endif 

set dir_str = `echo ${cwd} | sed 's#.*/##'`

setenv IVI_LOCK_TIME_STAMP "${IVI_LOCKFILE_DIR}/${lock_label}_${dir_str}.lock.$$"
${IVI_TOUCH} ${IVI_LOCK_TIME_STAMP}

if ($?IVI_LOCKFILE_NAME) then
    set looks_like_abs_path = `echo ${IVI_LOCKFILE_NAME} | grep -c '^/'`
    set looks_like_path = `echo ${IVI_LOCKFILE_NAME} | grep -c '/'`

    if (${looks_like_abs_path}) then
        set lock_file = "${IVI_LOCKFILE_NAME}"
    else if (${looks_like_path}) then
        set lock_file = "`pwd`/${IVI_LOCKFILE_NAME}"
    else 
        set lock_file = ${IVI_LOCKFILE_DIR}/${IVI_LOCKFILE_NAME}.lock
    endif 
else
    set lock_file = ${IVI_LOCKFILE_DIR}/${lock_label}_${dir_str}.lock
endif 

# Mostly for testing and debugging.
setenv IVI_LOCKFILE_PATH "${lock_file}"

onintr bail

# Best not to use VERBOSE_PSO here.
if ($?IVI_VERBOSE) then
    echo "${verbose_indent}Script ivi_lock_create process $$ running lock_create ${lock_label}"
    echo "${verbose_indent}    in `pwd` using lockfile '${lock_file}'"
endif

if ("${IVI_LOCKING_METHOD}" == "lockfile-progs") then
    # if ($?IVI_VERBOSE) then
        echo "Process $$ running lock_create ${lock_label} in `pwd` using lockfile-progs"
    # endif 
#     echo "Exiting with error as this is unfinished."
#     exit 1

    lockfile-create ${lock_file}

    if ((! ${status}) && ($?IVI_VERBOSE)) then
        echo "${verbose_indent}Process $$ successfully set ${lock_label} in `pwd` using lockfile-create."
    endif
else 
    # Fallback locking method. Doing locking robustly in scripts is hard, but
    # often (e.g., mac) we do not have a good solution. The following is better
    # than nothing.

    # Best not to use VERBOSE_PSO here.
    if ($?IVI_VERBOSE) then
        echo "${verbose_indent}Process $$ running lock_create ${lock_label} in `pwd` using fallback locking system."
    endif

    # Reduce the chances of everyone wanting to start at the same time.
    # I do not know how likely this is to fail, or even to return success
    # without doing the right thing, but it seems to work on the mac. 
    set x = $$ ; @ x %= 100 ; sleep 0.$x >& /dev/null
    if (${status}) then
        ${P_STDERR} "Fractional sleep failed in ivi_lock_create." 
        set exit_status = 1
        goto cleanup
    endif

    set ppid = `ps -p $$ -o ppid=`

    while (1) 
        if (! -e "${lock_file}") then
            echo ${ppid} >! ${lock_file}

            if ($?IVI_VERBOSE) then
                echo "${verbose_indent}Process $$ setting lock file ${lock_file}."
                # ps -l $$
            endif 
        else 
            set count = 0
            set got_it = 0

            while (-e "${lock_file}")
                set lock_proc = `cat ${lock_file}`

                # Happens! 
                if ($status) then 
                    echo ${ppid} >! ${lock_file}
                    echo "Lock file ${lock_file} disappeared! Taking the lock."
                    set got_it = 1
                    break
                endif 

                if ($?IVI_VERBOSE_2) then
                    echo "${verbose_indent}Lock file ${lock_file} exists."
                    echo "${verbose_indent}Checking on lock file process ${lock_proc}." 
                endif 

                ps ${lock_proc} >& /dev/null
                set ps_status = "${status}" 

                if (${ps_status}) then
                    echo ${ppid} >! ${lock_file}

                    if ($?IVI_VERBOSE) then
                        echo "${verbose_indent}Process $$ resetting lock file ${lock_file} for process ${ppid} as process ${lock_proc} does not exist."
                    endif 

                    set got_it = 1
                    break
                else if (${count} > 180) then
                    echo "Process $$ waited too long for ${lock_file}. "
                    echo "Information about the process holding the lock:"
                    ps -l ${lock_proc} 
                    echo "Sleeping for one minute for debugging" 
                    sleep 60
                    echo "Ignoring the lock."
                    echo ${ppid} >! ${lock_file}
                    set got_it = 1
                    break
                else  
                    if ($?IVI_VERBOSE) then
                        echo "${verbose_indent}Process $$ waiting for ${lock_file} held by process ${lock_proc} on behalf of process ${ppid} ."
                    endif 

                    # Add some fractional seconds to reduce the chances of everyone
                    # wanting to start at the same time.
                    set x = $$ ; @ x %= 100 ; sleep 1.$x >& /dev/null
                    if (${status}) then
                        ${P_STDERR} "Fractional sleep failed in ivi_lock_create." 
                        set exit_status = 1
                        goto cleanup
                    endif

                    @ count ++
                endif
            end

            # If we got_it is still false, then the lock file was removed by the
            # process we were waiting on.
            #
            if (! ${got_it}) then
                echo ${ppid} >! ${lock_file}

                if ($?IVI_VERBOSE) then
                    echo "${verbose_indent}Process $$ setting lock file ${lock_file} for process ${lock_proc} as the lock file no longer exists."
                endif 
            endif 
        endif 

        # Check for clash. 
        set lock_proc = `cat ${lock_file}`
        if (${status}) then
            # Could happn.
            echo "Lock file ${lock_file} disappeared. Trying again to get the lock."
        else if (${lock_proc} != ${ppid}) then 
            echo "Process ${lock_proc} beat us to it. Resuming waiting."
        else 
            break
        endif 
    end

    if ($?IVI_VERBOSE) then
        echo "${verbose_indent}Process $$ successfully set ${lock_label} on behalf of process ${ppid} in `pwd` using fallback locking method."
    endif
endif 

cleanup: 
    
${IVI_RM} ${IVI_LOCK_TIME_STAMP}

exit ${exit_status}

# We should not be here unless onintr

bail:

if (-e "${lock_file}") then 
    echo "Script ivi_lock_create process $$ removing lock file ${lock_file} because of interrupt."
    echo " "
    ${IVI_RM} "${lock_file}"
endif 

${IVI_RM} ${IVI_LOCK_TIME_STAMP}

exit 1


   
