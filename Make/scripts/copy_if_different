#!/bin/tcsh -f

set num_args = $#argv

if (! $?VERBOSE_INDENT) then
    setenv VERBOSE_INDENT ""
endif 

if ("${num_args}" != "2") then
    ${P_STDERR} "Script copy_if_different called with ${num_args} arguments."
    ${P_STDERR} "It should have exactly 2 arguments."
    exit 1
endif 

if (! -e "$1") then
    ${P_STDERR} "First arg to copy_if_different (${1}) is not a file."
    exit 1
endif 

if (-e "$2") then
    ${IVI_DIFF_TEST} $1 $2 >& /dev/null
    if (${status}) then 
        # Ensure time stamp is later than any file built by the process that
        # called this script. 
        
        set sleep_time = 0.01

        ${VERBOSE_ECHO} "${VERBOSE_INDENT}Script copy_if_different found a difference with args $1 and $2."
        ${VERBOSE_ECHO} "${VERBOSE_INDENT}Script copy_if_different is sleeping for ${sleep_time} seconds."

        sleep ${sleep_time}

        ${VERBOSE_ECHO} "${VERBOSE_INDENT}Script copy_if_different is executing ${IVI_CP} $1 $2."
        ${IVI_CP} "$1" "$2"
        if (${status}) exit 1 
    else 
        ${VERBOSE_ECHO_2} "${VERBOSE_INDENT}Skipping copying $1 to $2 because there is no difference."
    endif 
else
    ${VERBOSE_ECHO} "${VERBOSE_INDENT}Script copy_if_different is copying $1 to $2 because $2 does not exist."
    ${IVI_CP} "$1" "$2"
    if (${status}) exit 1 
endif 



