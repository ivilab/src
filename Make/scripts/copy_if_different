#!/bin/tcsh -f

set num_args = $#argv

if (! $?IVI_VERBOSE_INDENT) then
    setenv IVI_VERBOSE_INDENT ""
endif 

if ("${num_args}" != "2") then
    ${P_STDERR} "Script copy_if_different called with ${num_args} arguments."
    ${P_STDERR} "It should have exactly 2 arguments."
    exit 1
endif 

if (! -e $1) then
    ${P_STDERR} "First arg to copy_if_different (${1}) is not a file."
    exit 1
endif 

if (-e $2) then
    diff -q $1 $2 >& /dev/null
    if (${status}) then 
        # Ensure time stamp is later than any file built by the process that
        # called this script. 
        
        # This idea, copied from locking code, does not make sense here,
        # set x = $$ ; @ x %= 100 ; set sleep_time = "0.0${x}"
        set sleep_time = 0.01

        if ($?IVI_VERBOSE) then
            echo "${IVI_VERBOSE_INDENT}Script copy_if_different found a difference with args $1 and $2."
            echo "${IVI_VERBOSE_INDENT}Script copy_if_different is sleeping for ${sleep_time} seconds."
        endif

        sleep ${sleep_time}

        if ($?IVI_VERBOSE) then
            echo "${IVI_VERBOSE_INDENT}Script copy_if_different is executing /bin/cp -f $1 $2."
        endif

        /bin/cp -f $1 $2
    else if ($?IVI_VERBOSE_2) then
        echo "${IVI_VERBOSE_INDENT}Skipping copying $1 to $2 because there is no difference."
    endif 
else
    if ($?IVI_VERBOSE) then
        echo "${IVI_VERBOSE_INDENT}Script copy_if_different is copying $1 to $2 because $2 does not exist."
    endif

    /bin/cp -f $1 $2
endif 



