#!/bin/tcsh -f 

##############################################################################
#
# Identification:
#     Removes the semaphore that blocks cooperating processes from proceeding 
#
# Synopsis:
#     ivi_lock_create {label} [pid]
#
# Description:
#     This is the script of a pair for locking blocks of script code that is not
#     conveniently expressed as a command (otherwise use ivi_lock). It works
#     much as lockfile-remove, and in fact, is implemented using lockfile-remove
#     if it is available. The main justification for this script is that locking
#     is not universally available. Most notably good locking facilities are not
#     available in the mac OS. Hence, this script also implements a fall back
#     method that is less than ideal, but usually works. 
#
#     The argument to ivi_lock_remove needs to be the same as for the proceeding
#     call to lockfile-create (excluding its first argument). 
#
#     I might have called this script ivi_lock_destroy, but since it is similar
#     to the standard command lockfile-remove, I went with ivi_lock_remove. 
#
# Author:
#     Kobus Barnard 
#
##############################################################################

# We assume that init_scripts has been called as this script is largely used as
# part of the IVI build system reflected by the fact that it lives in Make.
# Thus, we have P_STDERR, VERBOSE_ECHO, and MAKE_SCRIPT_PATH. 

# This script must be kept in sync with ivi_lock_create
#
# set echo

if ($# < 1) then
    ${P_STDERR} "Script ivi_lock_remove needs at least one argument."
    exit 1
else if ($# < 2) then
    set requesting_pid = `ps -p $$ -o ppid=`
else 
    set requesting_pid = $2 
endif 

if ($?IVI_DONT_LOCK) then
    ${VERBOSE_ECHO} "${VERBOSE_INDENT}IVI_DONT_LOCK is set, so ivi_lock_remove is not doing anything"
    exit 0
endif

set lock_label = "${1}"

source ${MAKE_SCRIPT_PATH}set_lockfile_name
if (${status}) exit ${status} 

set exit_status = 0

onintr ivi_lock_remove_bail

${VERBOSE_ECHO} "${VERBOSE_INDENT}Script ivi_lock_remove process $$ running ivi_lock_remove ${lock_label} in ${cwd}."
${VERBOSE_ECHO} "${VERBOSE_INDENT}Script ivi_lock_remove process $$ using lockfile '${lock_file}'"

if (($?PREFER_LOCKFILE_PROGS) && ("${IVI_LOCKFILE_CREATE_PATH}" != "") && ("${IVI_LOCKFILE_REMOVE_PATH}" != "")) then
    ${VERBOSE_ECHO} "UNTESTED: Process $$ running ivi_lock_remove ${lock_label} in `pwd` using lockfile-progs"

    lockfile-remove --lock-name argument ${lock_file}

    if (! ${status}) then
        ${VERBOSE_ECHO} "${VERBOSE_INDENT}Process $$ successfully removed ${lock_file} in `pwd` using lockfile-remove."
    endif
else 
    # IVI locking method. Doing locking robustly in scripts is hard, but
    # often (e.g., mac) we do not have a good solution. The following is better
    # than nothing.

    ${VERBOSE_ECHO} "${VERBOSE_INDENT}Process $$ running ivi_lock_remove ${lock_label} in `pwd` using IVI locking system."

    if (-e "${lock_file}") then
        #  I have not found a safe way to ignore error output. 
        set lock_id = `cat ${lock_file}`
        set cat_status = ${status}

        if (! ${cat_status}) then 
            set lock_host = `echo ${lock_id} | ${IVI_SED} 's/-.*//'`
            if (${status}) then 
                ${WARN_ECHO} "Script ivi_lock_remove sed command to get host failed. This should never happen."
            endif 

            set lock_proc = `echo ${lock_id} | ${IVI_SED} 's/^.*-//'`
            if (${status}) then 
                ${WARN_ECHO} "Script ivi_lock_remove sed command to get pid failed. This should never happen."
            endif 
        endif 
            
        if (${cat_status}) then
            echo "Script ivi_lock_remove reports lock file ${lock_file} to be removed just got removed."
        else if (${lock_proc} != ${requesting_pid}) then 
            echo "Script ivi_lock_remove not removing lock file ${lock_file} for process ${requesting_pid} as it belongs to ${lock_proc}."
        else 
            ${VERBOSE_ECHO} "${VERBOSE_INDENT}Process $$ removing lock file ${lock_file}."
            ${IVI_RM} ${lock_file}
            if (${status}) then
                ${VERBOSE_ECHO} "${VERBOSE_INDENT}Process $$ failed to remove lock file ${lock_file}."
                exit ${status}
            endif 
        endif 
    else 
        ${VERBOSE_ECHO} "${VERBOSE_INDENT}Process $$ reports lock file to remove (${lock_file}) does not exist."
    endif

    ${VERBOSE_ECHO} "${VERBOSE_INDENT}Process $$ successfully ran ivi_lock_remove ${lock_label} in ${cwd} using IVI locking method."
endif 

cleanup: 

# Restore 
onintr
    
exit ${exit_status}

# We should not be here unless onintr

ivi_lock_remove_bail:

${VERBOSE_ECHO} "${VERBOSE_INDENT}Process $$ doing ivi_lock_remove got an interrupt."

# If we are not locking otest_ivi_lock_2_r even if we are, then the lock_file variable might not
# be set when we got interrupted.

if (${safe_to_lock}) then
    if (${?lock_file}) then 
        if (-e "${lock_file}") then 
            echo "Script ivi_lock_remove process $$ removing lock file ${lock_file} because of interrupt."
            echo " "
            ${IVI_RM} "${lock_file}"
        endif 
    endif 
endif 

exit 1


   
