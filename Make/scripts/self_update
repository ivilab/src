#!/bin/tcsh -f

# Atomic update via copy, followed by execute a command (e.g., make). 
# Likely it only makes sense to call this file using "exec"
#
# This script is not currently used:
#     This script was created for updating "build" automatically, but we
#     currently do not use it for that. Kobus is keeping this file around for a
#     bit, as it might be useful, if only as an example of updating a file that
#     is being executing. 

# This script cannot use P_STDERR as it might not be defined yet. However, since
# we are in src/Make/scripts, we will assume that IVI_SRC_PATH is valid.

if ($# < 2) then
    ${IVI_SRC_PATH}Make/scripts/p_stderr "Script self_update needs at least two arguments."
    exit 1
endif 

# We will control what happens if either file does not exist.
if (! -r "$1") then
    ${IVI_SRC_PATH}Make/scripts/p_stderr "Script self_update cannot find file: $1" 
    exit 1
else if (! -r "$2") then
    ${IVI_SRC_PATH}Make/scripts/p_stderr "Script self_update cannot find file: $2"
    exit 1
endif 

if ($?IVI_VERBOSE) then
    echo 'Script self_update is executing: '/bin/cp \"$2\" \"$1\" 
endif 

# Also, we cannot use IVI_CP, as it might not be defined yet.  
/bin/cp "$2" "$1"
if ($status) exit 1

if ($# > 2) then
    if ($?IVI_VERBOSE) then
        echo "Script self_update is executing: $argv[3-$#]" 
    endif 

    $argv[3-$#]
    if ($status) exit 1
endif

