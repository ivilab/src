#!/bin/tcsh -f

#
# Identification
#     A script to create a new ivi library component
#
# Arguments:
#      A name of a subdirectory to be added to the directory "lib".
#      This script must be run in the directory "lib". 
#
# Description
#     LIKELY OBSELETE, or at least needs revision!!! 
#
#     This script creates a new ivi library component with a few template files
#     and associated makefiles. It assumes that "Make" and "lib" are up to date.
#     Use "svn" to achieve this. 
#
#     The new component is built using the directory
#     src/examples/templates/library_template, which should exist if "examples"
#     is up to date. If that directory cannot be found in your src tree, then
#     various other places are searched, including remotely. 
#
#     To make a test directory for the component, use the following in the
#     component directory:
#          create_program test program_group_template
#

set echo

set remote_user = ""

set num_args = ${#argv}


set target_name = ""
set source_dir = ""

set source_name = "template"

set count = 1
while ($count <= $num_args)
    if ("${argv[$count]}" == "-u") then
        @ count += 1
        if (${count} > $num_args) then
            ${P_STDERR} "Missing user name after -u"
            exit 1 
        else 
            set remote_user = "${argv[$count]}@"
        endif 
    else if ("${target_name}" == "") then
        set target_name = "${argv[$count]}"
    else 
        echo "Extra argument ${argv[$count]} ignored" 
    endif 

    @ count += 1
end 

if ("${target_name}" == "") then
    ${P_STDERR} "This script needs exactly one non-option parameter for the library name."
    exit 1
endif 

set source_dir = "library_template/lib/template"

# echo $target_name
# echo $source_dir
# echo $remote_user

set kjb_environment = 0

if ($?KJB_ENVIRONMENT) then
    if ($KJB_ENVIRONMENT) then
        set kjb_environment = 1
    else 
        set kjb_environment = 0
    endif
endif 

if ( ! $kjb_environment) then
    setenv PATH "/bin:/usr/bin:/usr/local/bin:/usr/X11R6/bin:${PATH}"
endif 

set use_remote = 1

set src_dist_dirs = "~/src/examples/templates /net/v04/work/kobus/src/examples/templates ~kobus/src/examples/templates ~kobus/src_dist/latest/examples/templates"

foreach src_dist_dir (${src_dist_dirs}) 
    if (-e "${src_dist_dir}/${source_dir}") then
        set use_remote = 0
        break
    endif
end

if (${use_remote}) then
    set src_dist_dir = "${remote_user}v01.cs.arizona.edu:/home/kobus/src_dist/latest/examples/templates"
endif 

echo src_dist_dir is ${src_dist_dir}
echo ""

set dir = `pwd`
set dir = `echo $dir | sed 's/\/*$//'`
set dir = `echo $dir | sed 's/.*\///'`

if ("${dir}" != "lib") then
    ${P_STDERR} "You need to be in a directory \'lib\'"
    exit 1 
endif 

#############################################################################

# This next part is shared by all files in Make whose names begin with "build",
# AND scripts ivi_add_makefiles, create_library, and create_program. Updates
# should be propogated. 

if ($?FORCE_IVI_SRC_DIR) then 
    if (-e "${FORCE_IVI_SRC_DIR}/Make/init_compile") then 
        setenv IVI_SRC_PATH "${FORCE_IVI_SRC_DIR}"
    else 
        bash -c 'echo "Env var FORCE_IVI_SRC_DIR is set to ${FORCE_IVI_SRC_DIR}, but ${FORCE_IVI_SRC_DIR}/Make/init_compile does not exist." >&2'
        exit 1 
    endif 
else  
    # Use env var for IVI_DIRS for better error reporting via bash sub-shell.
    if ($?FORCE_IVI_DIR) then
        setenv IVI_DIRS "${FORCE_IVI_DIR}"
    else
        setenv IVI_DIRS ". kjb KJB ivi IVI src"
    endif 

    # Find out where we are in the source tree. In particular, we want to find a
    # directory that has one of the directories in IVI_DIRS as a subdir, which
    # itself has Make as a sub-dir and Make/init_compile as a file in that. 

    # We want to use pure "cd", and so if cd has been aliase'd we want to save that
    # fact and unalias cd. 
    #
    set cd_alias = `alias cd`
    if ("${cd_alias}" != "") then
       unalias cd
    endif 

    # We want to block going above the user's home directory because it might cause
    # a mount request that we don't want. If we are not below their home, then it is
    # fine to ga to the root. 
    #
    pushd ${HOME} >& /dev/null
    set home_pwd = `pwd`
    popd >& /dev/null

    set found = 0
    set ivi_top = ""

    pushd `pwd` > /dev/null

    while (("`pwd`" != "/") && ("`pwd`" != "${home_pwd}")) 
        foreach ivi_dir (${IVI_DIRS}) 
            if ("${ivi_dir}" == ".") then
                set ivi_dir = ""
            else
                set ivi_dir = "${ivi_dir}/"
            endif 

            if (-e "${ivi_dir}Make/init_compile") then
                set found = 1
                break
            endif 
        end

        if (${found}) break

        set ivi_top = "../${ivi_top}"
        cd ..
    end

    popd > /dev/null

    # Restore cd alias.
    if ("${cd_alias}" != "") then
       alias cd "$cd_alias"
    endif 

    if (${found}) then
        if ("${ivi_dir}" == "") then
            set ivi_dir = "./"
        endif 

        setenv IVI_SRC_PATH "${ivi_top}${ivi_dir}"
    else 
        # We cannot use P_STDERR here because we might not have defined it yet.
        bash -c 'echo " " >&2'
        bash -c 'echo "This directory is not below a directory with IVI installed." >&2'
        bash -c 'echo "Valid directories for IVI in this context: ${IVI_DIRS}." >&2'
        bash -c 'echo "To experiment with a temporary fix, try setting FORCE_IVI_SRC_DIR." >&2'
        bash -c 'echo "For a more permanent fix, edit $0 or contact Kobus." >&2'
        exit 1
    endif 
endif 

pushd ${IVI_SRC_PATH} > /dev/null
    setenv IVI_SRC_PATH "${cwd}/"
popd > /dev/null

##############################################################################


if ($?IVI_VERBOSE) then
    echo "Script $0 is sourcing ${IVI_SRC_PATH}Make/init_compile"
endif 

source ${IVI_SRC_PATH}/Make/init_compile

if (${status}) then 
    echo "Sourcing ${IVI_SRC_PATH}/Make/init_compile returned failure."
    exit 1
endif 

if ($?IVI_VERBOSE) then
    echo "Sourcing ${IVI_SRC_PATH}Make/init_compile finished successfully."
endif 

echo "Now:"
echo "   IVI_SRC_PATH: ${IVI_SRC_PATH}"
echo "   MAKE_PATH: ${MAKE_PATH}"
echo "   MAKE_SCRIPT_PATH: ${MAKE_SCRIPT_PATH}"
echo " "

##############################################################################
#############################################################################

if (-e "${target_name}") then
    ${P_STDERR} "${target_name} already exists. Giving up."
    exit 1
endif 

mkdir -p ${target_name}

echo ${source_dir}
echo ${target_name}
pwd

rsync -e ssh -v -rlpgoDu --include='*.[ch]' --exclude='*' ${src_dist_dir}/${source_dir}/ ${target_name}

if (${status}) then
    ${P_STDERR} "rsync failed in create_library."
    exit 1
endif 

pushd ${target_name} > /dev/null

set uc_source_name = `echo $source_name | sed "y/abcdefghijklmnopqrstuvwxyz/ABCDEFGHIJKLMNOPQRSTUVWXYZ/"`
set uc_target_name = `echo $target_name | sed "y/abcdefghijklmnopqrstuvwxyz/ABCDEFGHIJKLMNOPQRSTUVWXYZ/"`

foreach file (${source_name}*)
    set new_file = `echo $file | sed "s/${source_name}/${target_name}/"`
    ${IVI_MV} $file $new_file
    ${VIM_FOR_MAKE} -e -s -c ':%s/'${source_name}'/'${target_name}'/g' -c :wq ${new_file} < /dev/null >& /dev/null
    ${VIM_FOR_MAKE} -e -s -c ':%s/'${uc_source_name}'/'${uc_target_name}'/g' -c :wq ${new_file}  < /dev/null >& /dev/null
end

${MAKE_SCRIPT_PATH}ivi_add_makefiles


