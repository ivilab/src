#!/bin/tcsh -f

##################################################################################
#

if ($# != 1) then
    ${P_STDERR} "Script build_sub_libs_needed_4 takes just 1 arg. Called with $*"
    exit 1
endif 

set recurse_dir_str = "${1}"

set exit_status = 0

${VERBOSE_ECHO_2} " "
${VERBOSE_ECHO_2} "${VERBOSE_INDENT}------- build_sub_libs_needed_4 considering ${recurse_dir_str} ----------"
${VERBOSE_ECHO_2} " "

pushd ${recurse_dir_str} > /dev/null
    ${IVI_MKDIR} Include_lines
    if (${status}) then
        set exit_status = 1
        goto end_of_script
    endif 

    set too_old = 1
    if ((-e "Include_lines/include_lines") && (-e "Include_lines/include_lines.new") && (-e "Include_lines/file_list")) then
        set too_old = `${MAKE_SCRIPT_PATH}test_older_than Include_lines/include_lines.new Include_lines/file_list`

        if (${too_old}) then
            ${VERBOSE_ECHO_2} "${VERBOSE_INDENT}------- In ${cwd}/Include_lines, include_lines.new is older than file_list (need to build)  ----------"
        endif 
    else 
        ${VERBOSE_ECHO_2} "${VERBOSE_INDENT}------- In ${cwd}/Include_lines, one of include_lines, include_lines.new, or file_list does not exist (need to build)  ----------"
    endif

    if (${too_old}) then
        ${VERBOSE_ECHO_2} "${VERBOSE_INDENT}------- Script build_sub_libs_needed_4 calling build_file_list_2 to build ${cwd}/Include_lines/include_lines ----------"
        ${MAKE_SCRIPT_PATH}ivi_lock_for_make 120 Include_lines/include_lines ${MAKE_SCRIPT_PATH}build_file_list_2
        if (${status}) then
            set exit_status = 1
            goto end_of_script
        endif 
    else
        ${VERBOSE_ECHO_2} "${VERBOSE_INDENT}------- Script build_sub_libs_needed_4 able to SKIP calling build_file_list_2 to build ${cwd}/Include_lines/include_lines ----------"
    endif 

    set need_to_build = 0

    # These tests should mimic the ones below checked after trying to build. 

    if (! -e "Include_lines/sub_libs_needed") then
        ${VERBOSE_ECHO_2} "${VERBOSE_INDENT}------- ${cwd}/Include_lines/sub_libs_needed does not exist (need to build)  ----------"
        set need_to_build = 1
    else  
        set too_old = `${MAKE_SCRIPT_PATH}test_older_than Include_lines/sub_libs_needed Include_lines/include_lines`
        if (${status}) then
            set exit_status = 1
            goto end_of_script
        endif 

        if (${too_old}) then
            ${VERBOSE_ECHO_2} "${VERBOSE_INDENT}------- ${cwd}/Include_lines/sub_libs_needed is too old (need to build) ----------"
            set need_to_build = 1
        endif 
    endif 

    if (! ${need_to_build}) then
        if ($?FORCE_BUILD_SUB_LIBS) then
            ${VERBOSE_ECHO_2} "${VERBOSE_INDENT}------- ${cwd}/Include_lines/sub_libs_needed needs to be built as we are FORCING doing so ----------"
            set need_to_build = 1
        endif 
    endif 

    if (${need_to_build}) then
        ${VERBOSE_ECHO} "${VERBOSE_INDENT}------- build_sub_libs_needed_4 building sub_libs_needed in ${recurse_dir_str} ----------"
        ${VERBOSE_ECHO} " "

        # This is the recursive call, as build_include_lines_2 calls # build_sub_libs_needed.
        
        # 893 is a unique approximation of 900
        ${MAKE_SCRIPT_PATH}ivi_lock_for_make 893 Include_lines/sub_libs_needed ${MAKE_SCRIPT_PATH}build_include_lines_2
        if (${status}) then
            ${P_STDERR} "Script build_include_lines_2 reported error to build_sub_libs_needed_4 in ${cwd}."
            set exit_status = 1
            goto end_of_script
        endif 
    else 
        ${VERBOSE_ECHO} " "
        ${VERBOSE_ECHO} "${VERBOSE_INDENT}------- Directory ${recurse_dir_str} already visited recently (no recursion needed) ----------"
        ${VERBOSE_ECHO} " "
    endif 

    set recurse_success = 0

    # These tests should mimic the ones above leading to the attempt to build. 

    if (! -e "Include_lines/sub_libs_needed") then
        ${P_STDERR} "After recursion ${cwd}/Include_lines/sub_libs_needed still does not exist"
    else
        set too_old = `${MAKE_SCRIPT_PATH}test_older_than Include_lines/sub_libs_needed Include_lines/include_lines`
        if (${status}) then
            ${P_STDERR} "In ${cwd} test_older_than failed."
            ${P_STDERR} "Likely either Include_lines/sub_libs_needed or Include_lines/include_lines does not exist."
            set exit_status = 1
            goto end_of_script
        endif 

        if (${too_old}) then
            ${P_STDERR} "After recursion ${cwd}/Include_lines/sub_libs_needed is still too old"
            ${P_STDERR} "Script build_sub_libs_needed_4 reports timestamp for ${cwd}/Include_lines/include_lines: `${IVI_TIMESTAMP} ${cwd}/Include_lines/include_lines`" 
            ${P_STDERR} "Script build_sub_libs_needed_4 reports timestamp for ${cwd}/Include_lines/sub_libs_needed: `${IVI_TIMESTAMP} ${cwd}/Include_lines/sub_libs_needed`" 
        else 
            ${VERBOSE_ECHO_2} "${VERBOSE_INDENT}------- After recursion ${cwd}/Include_lines/sub_libs_needed is OK ----------"
            set recurse_success = 1
        endif 
    endif 

    if (! ${recurse_success}) then
        set exit_status = 1
        ${P_STDERR} "Build of sub_libs_needed by build_sub_libs_needed_4 FAILED in ${cwd}."
        goto end_of_script
    endif 
popd  > /dev/null

end_of_script: 

${VERBOSE_ECHO} "${VERBOSE_INDENT}Leaving script build_sub_libs_needed_4 with status ${exit_status} after processing ${recurse_dir_str} from ${cwd}."

exit ${exit_status}

