#!/bin/tcsh -f

##################################################################################
#
# This script exists simply because if we called ivi_lock_for_make directly,
# then we would not capture its exit status and result. 


if ($# == 2) then
   set output_file = ""
else if ($# == 3) then
   set output_file = "$3"
else 
    ${P_STDERR} "Script build_sub_libs_needed_3 takes either 2 or 3 args. Called with $*"
    exit 1
endif 

${VERBOSE_ECHO} "${VERBOSE_INDENT}Entering script build_sub_libs_needed_3 in ${cwd} with args $*"

set recurse_dir_str = "${1}"
set status_file = "${2}"
set output_file = "${3}"

if (-e "${status_file}") then
    ${VERBOSE_ECHO} "${VERBOSE_INDENT}Script build_sub_libs_needed_3 exiting early due to existing ${status_file}."
    exit 0
endif

set exit_status = 0

# 860 is a unique approximation of 900 for easier grep. We redirect to a unique
# output file so we can check that we do not have a locking issue. 
#
if ("${output_file}" == "") then
    ${MAKE_SCRIPT_PATH}ivi_lock_for_make 860 ${recurse_dir_str}/Include_lines/sub_libs_needed ${MAKE_SCRIPT_PATH}build_sub_libs_needed_4 "${recurse_dir_str}" 
    if (${status}) set exit_status = 1
else
    ${MAKE_SCRIPT_PATH}ivi_lock_for_make 860 ${recurse_dir_str}/Include_lines/sub_libs_needed ${MAKE_SCRIPT_PATH}build_sub_libs_needed_4 "${recurse_dir_str}" |& tee ${output_file}.$$
    if (${status}) set exit_status = 1
endif 

if (-e "${status_file}") then
    ${P_STDERR} " "
    ${P_STDERR} "Possible LOCKING FAILURE" 
    ${P_STDERR} "Script build_sub_libs_needed_3 found a preexising ${status_file}."
    ${P_STDERR} "This should be IMPOSSIBLE because we run under ivi_lock." 
    ${P_STDERR} "Previous status was `${IVI_CAT} ${status_file}`." 
    ${P_STDERR} " " 

    if ("${output_file}" != "") then
        if (-e "${output_file}") then
            ${P_STDERR} "Previous debugging output file moved to ${output_file}.previous.$$" 
            ${IVI_MV} "${output_file}" "${output_file}.previous.$$"
            ${P_STDERR} " " 
        endif 
    endif 
endif

# ${IVI_CAT} "${output_file}.$$"

if ("${output_file}" != "") then
    ${IVI_MV} "${output_file}.$$" "${output_file}"
    if (${status}) set exit_status = 1
endif

${IVI_ECHO} ${exit_status} > ${status_file}
if (${status}) then 
    ${VERBOSE_ECHO} "${VERBOSE_INDENT}Writing ${status_file} FAILED."
    set exit_status = 1
endif 

${VERBOSE_ECHO} "${VERBOSE_INDENT}Leaving script build_sub_libs_needed_3 with status ${exit_status} in ${cwd}."

if (${exit_status}) then
    ${P_STDERR} "Parallle build of sub_libs_needed by build_sub_libs_needed_3 FAILED in ${cwd}."
endif 

exit ${exit_status}

