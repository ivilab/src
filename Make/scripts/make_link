#!/bin/tcsh -f 

##############################################################################
#
# Identification:
#     A script to create symbolic links with a few extra features
#
# Description:
#     This script creates symbolic links, but setting up the call to ${IVI_LN}
#     (which is usually something like "ln -s -f"). It currently serves other
#     scripts, mostly for the build system, and so we assume that init_compile
#     has been sourced.  
#
#     If the second argument is a link, and it is the one we are trying to
#     change it to (first argument), we do nothing. This means that even if
#     multiple jobs are trying to make the link, we do it only once. If the
#     second argument is a link, but different from the second argument, we
#     replace it. If it is a directory, we move it to a safe place with message,
#     and create the link in its place. 
#
# Author:
#     Kobus Barnard 
#
##############################################################################

if ($# != 2) then
    ${P_STDERR} "Script make_link needs two arguments. Called with args: $*."
    exit 1
endif 

# set echo

set link_target = "$1"
set link_name = "$2"

if (-e "${link_name}") then
    if (! -l "${link_name}") then
        ${WARN_ECHO} "Proposed link ${link_name} exists and is not a link."
        ${WARN_ECHO} "Forcing moving of ${link_name} to ${link_name}.$$ reduce confusion due to apparently being up to date."

        ${IVI_MV} "${link_name}" "${link_name}.$$"
        if (${status}) then
            ${P_STDERR} "Script make_link command failed in ${cwd}: ${IVI_MV} ${link_name} ${link_name}.$$"
            exit 1
        endif 
    else 
        set link_to = `${MAKE_SCRIPT_PATH}get_link_target "${link_name}"`
        
        if ("${link_to}" == "${link_target}") then
            ${VERBOSE_ECHO} "Link from ${link_name} to ${link_target} already exists in `pwd`"
            ${VERBOSE_ECHO} "Early exit from make_link."
            exit 0
        else 
            ${VERBOSE_ECHO} "Removing link '${link_name}' in  `pwd`"

            ${IVI_RM} "${link_name}"
            if (${status}) then
                ${P_STDERR} "Script make_link command failed in ${cwd}: ${IVI_RM} ${link_name}"
                exit 1
            endif 
        endif 
    endif 
else 
    ${VERBOSE_ECHO} "'${link_name}' does not exist in `pwd`"
endif 

${VERBOSE_ECHO} "Script make_link executing in ${cwd}: ${IVI_LN} ${link_target} ${link_name}"

${IVI_LN} ${link_target} ${link_name}
if (${status}) then
    ${P_STDERR} "Script make_link command failed in ${cwd}: ${IVI_LN} ${link_target} ${link_name}"
    exit 1
endif 


