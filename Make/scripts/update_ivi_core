#!/bin/tcsh -f

##############################################################################
#
# Identification:
#     A script to update core IVI software
#
# Description:
#     This script updates the core IVI software using SVN. The "core" system is
#     the build system, the library, some utility programs ("tools"), and some
#     example programs. It currently excludes programs for research projects,
#     but this might change. Alternatively, we might write a script
#     "update_ivi_all". 
#
#     This script must be run in in a directory in your source directory.
#     Typically, that is $HOME/src, but it need not be, especially if you feel
#     the need to have multiple versions. 
#
#     If you have already checked out "Make" into your source directory, and you
#     are in that directory, then this script can be run as:
#     |    Make/scripts/update_ivi_core
#     Alternatively, and probably ideally, some instance of "Make/scripts" is in
#     your path. 
#
# Author:
#     Kobus Barnard 
#
##############################################################################

# This next part is shared by all files in Make whose names begin with "build",
# AND scripts ivi_add_makefiles, create_library, create_program, and
# update_ivi_core. Updates should be propogated. 

# We want to use a pure "cd".
unalias cd
    
if ($?FORCE_IVI_SRC_DIR) then 
    if (-e "${FORCE_IVI_SRC_DIR}/Make/init_compile") then 
        setenv IVI_SRC_PATH "${FORCE_IVI_SRC_DIR}"
    else 
        bash -c 'echo "Env var FORCE_IVI_SRC_DIR is set to ${FORCE_IVI_SRC_DIR}, but ${FORCE_IVI_SRC_DIR}/Make/init_compile does not exist." >&2'
        exit 1 
    endif 
else  
    # Use env var for IVI_DIRS for better error reporting via bash sub-shell.
    if ($?FORCE_IVI_DIR) then
        setenv IVI_DIRS "${FORCE_IVI_DIR}"
    else
        setenv IVI_DIRS ". kjb KJB ivi IVI src"
    endif 

    # Find out where we are in the source tree. In particular, we want to find a
    # directory that has one of the directories in IVI_DIRS as a subdir, which
    # itself has Make as a sub-dir and Make/init_compile as a file in that. 

    # We want to block going above the user's home directory because it might cause
    # a mount request that we don't want. If we are not below their home, then it is
    # fine to ga to the root. 
    #
    pushd ${HOME} >& /dev/null
    set home_pwd = `pwd`
    popd >& /dev/null

    set found = 0
    set ivi_top = ""

    pushd `pwd` > /dev/null

    while (("`pwd`" != "/") && ("`pwd`" != "${home_pwd}")) 
        foreach ivi_dir (${IVI_DIRS}) 
            if ("${ivi_dir}" == ".") then
                set ivi_dir = ""
            else
                set ivi_dir = "${ivi_dir}/"
            endif 

            if (-e "${ivi_dir}Make/init_compile") then
                set found = 1
                break
            endif 
        end

        if (${found}) break

        set ivi_top = "../${ivi_top}"
        cd ..
    end

    popd > /dev/null

    if (${found}) then
        if ("${ivi_dir}" == "") then
            set ivi_dir = "./"
        endif 

        setenv IVI_SRC_PATH "${ivi_top}${ivi_dir}"
    else 
        setenv THIS_PROGRAM "$0" 

        # We cannot use P_STDERR here because we might not have defined it yet.
        bash -c 'echo " " >&2'
        bash -c 'echo "This directory is not below a directory with IVI installed." >&2'
        bash -c 'echo "Valid directories for IVI in this context: ${IVI_DIRS}." >&2'
        bash -c 'echo "To experiment with a temporary fix, try setting FORCE_IVI_SRC_DIR." >&2'
        bash -c 'echo "For a more permanent fix, edit this program: " >&2'
        bash -c 'echo "    ${THIS_PROGRAM} " >&2'
        bash -c 'echo "or contact Kobus." >&2'
        bash -c 'echo " " >&2'

        exit 1
    endif 
endif 

cd ${IVI_SRC_PATH} 

svn co svn+ssh://vision/home/svn/src/Make/trunk Make
svn co svn+ssh://vision/home/svn/src/lib/trunk lib
svn co svn+ssh://vision/home/svn/src/include_before/trunk include_before
svn co svn+ssh://vision/home/svn/src/include_after/trunk include_after
svn co svn+ssh://vision/home/svn/src/IVI/trunk IVI
svn co svn+ssh://vision/home/svn/src/IVI_cpp/trunk IVI_cpp
svn co svn+ssh://vision/home/svn/src/examples/trunk examples
